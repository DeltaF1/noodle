(
	app/noodle : illustration program
	
	right-click - erase
	alt-click - drag canvas
	arrows - move zoom
	space - toogle zoom
	backspace - blank canvas
	1-8 - select brush size
)

%+  { ADD } %-   { SUB }  %*  { MUL } %/   { DIV }
%<  { LTH } %>   { GTH }  %=  { EQU } %!   { NEQ }
%++ { ADD2 } %-- { SUB2 } %** { MUL2 } %// { DIV2 }
%<< { LTH2 } %>> { GTH2 } %== { EQU2 } %!! { NEQ2 }

%RTN  { JMP2r }
%SFL  { #40 SFT SFT }
%TOS  { #00 SWP } %TOB { SWP POP }
%ABS2 { DUP2 #000f SFT2 = #04 JCN #ffff ** }
%LTS2 { #8000 ++ SWP2 #8000 ++ >> }
%GTS2 { #8000 ++ SWP2 #8000 ++ << }
%MOD8 { #0007 AND2  }
%INCR2 { SWP2 #0001 ++ SWP2 }
%DEBUG  { .Console/byte DEO #0a .Console/char DEO }
%DEBUG2 { .Console/short DEO2 #0a .Console/char DEO }
%TOGGLE { DUP LDZ #00 = SWP STZ }
%FILESIZE { .canvas/w LDZ2 .canvas/h LDZ2 ** #0008 ** }
%RELEASE-MOUSE { #00 .Mouse/state DEO }

( devices )

|00 @System     [ &vector $2 &pad     $6 &r      $2 &g     $2 &b     $2 ]
|10 @Console    [ &vector $2 &pad     $6 &char   $1 &byte  $1 &short $2 &string $2 ]
|20 @Screen     [ &vector $2 &width   $2 &height $2 &pad   $2 &x     $2 &y $2 &addr $2 &color $1 ]
|80 @Controller [ &vector $2 &button  $1 &key    $1 ]
|90 @Mouse      [ &vector $2 &x       $2 &y      $2 &state $1 &chord $1 ]
|a0 @File       [ &vector $2 &success $2 &offset $2 &pad   $2 &name  $2 &length $2 &load $2 &save $2 ]

( variables )

|0000

@color $1 

@state
	&timer $1
	&trap $1
	&blink $1
	&presentation $1
@theme
	&r0 $2 &g0 $2 &b0 $2 
	&r1 $2 &g1 $2 &b1 $2 
@path
	&length $1 
	&name $20
@brush 
	&tool $1 
	&size $1 
	&patt $1 
	&oper $2 
	&mode $1
@cursor 
	&x $2 
	&y $2 
	&x0 $2 
	&y0 $2 
	&last $1 
@zoom 
	&active $1 
	&x $2 
	&y $2

@toolpane [ &x1 $2 &y1 $2 &x2 $2 &y2 $2 ]
@pattpane [ &x1 $2 &y1 $2 &x2 $2 &y2 $2 ]
@sizepane [ &x1 $2 &y1 $2 &x2 $2 &y2 $2 ]
@viewpane [ &x1 $2 &y1 $2 &x2 $2 &y2 $2 ]
@canvas [ &x1 $2 &y1 $2 &x2 $2 &y2 $2 &w $2 &h $2 ]
@rect [ &x1 $2 &y1 $2 &x2 $2 &y2 $2 ]
@line [ &x1 $2 &y1 $2 &x2 $2 &y2 $2 &sx $2 &sy $2 &dx $2 &dy $2 &e1 $2 &e2 $2 ]
@pt0 [ &x $2 &y $2 ]
@pt1 [ &x $2 &y $2 ]
@pt2 [ &x $2 &y $2 ] ( paint-rect )
@px [ &x $1 &y $1 ]

( program )

|0100
	
	( theme )
	#e0f7 .theme/r0 STZ2 
	#30fc .theme/g0 STZ2 
	#30f9 .theme/b0 STZ2 

	( normal mode )
	#00fe .theme/r1 STZ2 
	#00f3 .theme/g1 STZ2 
	#00f3 .theme/b1 STZ2

	( vectors ) 
	;on-frame .Screen/vector DEO2
	;on-button .Controller/vector DEO2
	;on-mouse .Mouse/vector DEO2

	.theme/r0 LDZ2 .System/r DEO2 
	.theme/g0 LDZ2 .System/g DEO2 
	.theme/b0 LDZ2 .System/b DEO2

	( default size )
	.Screen/width DEI2 #0008 // #0004 -- .canvas/w STZ2 
	.Screen/height DEI2 #0008 // #0006 -- .canvas/h STZ2

	( default brush )
	#04 .brush/size STZ 
	#00 .brush/patt STZ 
	#00 .brush/tool STZ

	( load file )
	;untitled-txt ;path/name ;strcpy JSR2

	( setup panes )
	#0011 .toolpane/x1 STZ2 
	#000f .toolpane/y1 STZ2 
	.toolpane/x1 LDZ2 #0028 ++ .toolpane/x2 STZ2 
	.toolpane/y1 LDZ2 #0008 ++ .toolpane/y2 STZ2
	#0040 .sizepane/x1 STZ2 
	#000f .sizepane/y1 STZ2 
	.sizepane/x1 LDZ2 #0040 ++ .sizepane/x2 STZ2 
	.sizepane/y1 LDZ2 #0008 ++ .sizepane/y2 STZ2
	.Screen/width DEI2 #0078 -- .viewpane/x1 STZ2 
	#000f .viewpane/y1 STZ2 
	.viewpane/x1 LDZ2 #0020 ++ .viewpane/x2 STZ2 
	.viewpane/y1 LDZ2 #0008 ++ .viewpane/y2 STZ2
	.Screen/width DEI2 #0050 -- .pattpane/x1 STZ2 
	#000f .pattpane/y1 STZ2 
	.pattpane/x1 LDZ2 #0040 ++ .pattpane/x2 STZ2 
	.pattpane/y1 LDZ2 #0008 ++ .pattpane/y2 STZ2

	( ready. )
	;center JSR2
	;clear JSR2

	( load default file )
	;path/name ;load-file JSR2
	#0c .path/length STZ

BRK

@on-frame ( -> )
	
	.state/trap LDZ #00 ! #01 JCN [ BRK ]

	( incr ) .state/timer LDZ #01 + DUP .state/timer STZ 

	#08 ! ,&no-toggle JCN
		.state/blink TOGGLE
		#21 .state/blink LDZ #05 * + ;draw-filepath JSR2
		#00 .state/timer STZ 
	&no-toggle
	
BRK

@on-mouse ( -> )

	;draw-cursor JSR2

	( on mouse-down )
	.Mouse/state DEI .cursor/last LDZ #0100 !! ,&no-mouse-down JCN
		.Mouse/x DEI2 .cursor/x0 STZ2
		.Mouse/y DEI2 .cursor/y0 STZ2
		.state/presentation LDZ ,&skip-panes JCN
			.Mouse/x DEI2 .Mouse/y DEI2 .sizepane ;within-rect JSR2
				;on-touch-sizepane JCN2
			.Mouse/x DEI2 .Mouse/y DEI2 .pattpane ;within-rect JSR2
				;on-touch-pattpane JCN2
			.Mouse/x DEI2 .Mouse/y DEI2 .toolpane ;within-rect JSR2
				;on-touch-toolpane JCN2
			.Mouse/x DEI2 .Mouse/y DEI2 .viewpane ;within-rect JSR2
				;on-touch-viewpane JCN2
		&skip-panes
		.Mouse/y DEI2 #0008 // TOB .Screen/height DEI2 #0008 // TOB #02 - =
			;on-touch-background JCN2
	&no-mouse-down

	( on mouse-up )
	.Mouse/state DEI .cursor/last LDZ #0001 !! ,&no-mouse-up JCN
		.brush/tool LDZ #02 ! ,&no-complete-line JCN
			.cursor/x0 LDZ2 .canvas/x1 LDZ2 -- 
			.cursor/y0 LDZ2 .canvas/y1 LDZ2 -- 
			.Mouse/x DEI2 .canvas/x1 LDZ2 -- 
			.Mouse/y DEI2 .canvas/y1 LDZ2 -- 
				;paint-line JSR2
			( update ) .Mouse/state DEI .cursor/last STZ
			BRK
		&no-complete-line
		.brush/tool LDZ #03 ! ,&no-touch-rect JCN
			.cursor/x0 LDZ2 .canvas/x1 LDZ2 -- 
			.cursor/y0 LDZ2 .canvas/y1 LDZ2 -- 
			.Mouse/x DEI2 .canvas/x1 LDZ2 -- 
			.Mouse/y DEI2 .canvas/y1 LDZ2 -- 
				;paint-rect JSR2
			( update ) .Mouse/state DEI .cursor/last STZ
			BRK
		&no-touch-rect
	&no-mouse-up

	.Mouse/state DEI DUP .cursor/last STZ #00 ! #01 JCN [ BRK ]

	.Mouse/x DEI2 .Mouse/y DEI2 .canvas ;within-rect JSR2
		;on-touch-canvas JCN2

	( toggle present mode )
	.cursor/x0 LDZ2 .cursor/y0 LDZ2 .canvas ;within-rect JSR2 ,&no-present JCN
		;present JSR2
		RELEASE-MOUSE
	&no-present

BRK

@on-touch-canvas ( -> )
	
	( set erase from right-click/mode )
	;remove-pixel .Mouse/state DEI #01 > .brush/mode LDZ + ,&no-rightclick JCN 
		POP2 ;add-pixel &no-rightclick 
	.brush/oper STZ2

	.brush/tool LDZ ,&no-touch-pen JCN
		.cursor/x0 LDZ2 .canvas/x1 LDZ2 -- 
		.cursor/y0 LDZ2 .canvas/y1 LDZ2 -- 
		.Mouse/x DEI2 .canvas/x1 LDZ2 -- 
		.Mouse/y DEI2 .canvas/y1 LDZ2 -- 
		;paint-line JSR2
		.Mouse/x DEI2 .cursor/x0 STZ2
		.Mouse/y DEI2 .cursor/y0 STZ2
		BRK &no-touch-pen

	.brush/tool LDZ #01 ! ,&no-touch-brush JCN
		.Mouse/x DEI2 .canvas/x1 LDZ2 -- 
		.Mouse/y DEI2 .canvas/y1 LDZ2 -- 
			;paint-brush JSR2
		BRK &no-touch-brush

	.brush/tool LDZ #04 ! ,&no-touch-zoom JCN
		.zoom/active LDZ #00 = .zoom/active STZ
		( release ) #00 .Mouse/state DEO
		.Mouse/x DEI2 .canvas/x1 LDZ2 -- .canvas/w LDZ2 #0001 SFT2 -- .zoom/x STZ2
		.Mouse/y DEI2 .canvas/y1 LDZ2 -- .canvas/h LDZ2 #0001 SFT2 -- .zoom/y STZ2
		;redraw JSR2 RELEASE-MOUSE &no-touch-zoom

BRK

@on-touch-background ( -> )
	
	#ff .Mouse/x DEI2 #0008 // TOB .Screen/width DEI2 #0008 // TOB - - 
	DUP #02 ! ,&no-save JCN
		;path/name ;save-file JSR2
		RELEASE-MOUSE POP BRK &no-save
	DUP #04 ! ,&no-load JCN
		;path/name ;load-file JSR2
		;draw-canvas JSR2
		RELEASE-MOUSE POP BRK &no-load
	DUP #05 ! ,&no-blank JCN
		;clear-file JSR2
		;redraw JSR2
		RELEASE-MOUSE POP BRK &no-blank
	POP
	.Mouse/x DEI2 #0008 // TOB .Screen/width DEI2 #0008 // TOB #05 - > ,&no-rename JCN
		.state/trap TOGGLE
		#00 .state/blink STZ 
		#21 ;draw-filepath JSR2
		RELEASE-MOUSE BRK &no-rename

BRK

@on-touch-sizepane ( -> )

	.Mouse/x DEI2 .sizepane/x1 LDZ2 -- #0003 SFT2 TOB 

	DUP .brush/size LDZ ! ,&no-toggle JCN
		.brush/mode LDZ #00 = .brush/mode STZ
	&no-toggle

	.brush/size STZ

	#01 .brush/tool STZ

	( draw ) 
	;draw-sizepane JSR2
	;draw-toolpane JSR2

	RELEASE-MOUSE

BRK

@on-touch-pattpane ( -> )
	
	.Mouse/x DEI2 .pattpane/x1 LDZ2 -- #0003 SFT2 TOB .brush/patt STZ

	#01 .brush/tool STZ

	( draw ) 
	;draw-pattpane JSR2
	;draw-toolpane JSR2

	RELEASE-MOUSE

BRK

@on-touch-toolpane ( -> )
	
	.Mouse/x DEI2 .toolpane/x1 LDZ2 -- #0003 SFT2 TOB .brush/tool STZ

	( draw ) 
	;draw-toolpane JSR2

	RELEASE-MOUSE

BRK

@on-touch-viewpane ( -> )
	
	.Mouse/x DEI2 .viewpane/x1 LDZ2 -- #0003 SFT2 TOB
	DUP ,&no-incwidth JCN
		.canvas/w LDZ2 .Screen/width DEI2 #0008 // #0001 -- >> ,&no-incwidth JCN
			.canvas/w LDZ2 #0001 ++ .canvas/w STZ2 
			;center JSR2 RELEASE-MOUSE &no-incwidth
	DUP #01 ! ,&no-decwidth JCN
		.canvas/w LDZ2 #0002 << ,&no-decwidth JCN
			.canvas/w LDZ2 #0001 -- .canvas/w STZ2 
			;center JSR2 RELEASE-MOUSE &no-decwidth
	DUP #02 ! ,&no-incheight JCN
		.canvas/h LDZ2 .Screen/height DEI2 #0008 // #0007 -- >> ,&no-incheight JCN
			.canvas/h LDZ2 #0001 ++ .canvas/h STZ2 
			;center JSR2 RELEASE-MOUSE &no-incheight
	DUP #03 ! ,&no-decheight JCN
		.canvas/h LDZ2 #0002 << ,&no-decheight JCN
			.canvas/h LDZ2 #0001 -- .canvas/h STZ2
			;center JSR2 RELEASE-MOUSE &no-decheight
	POP

BRK

@on-button ( -> )

	.Controller/key DEI #00 ! .state/trap LDZ #0101 == ;on-button-trap JCN2
	
	( control zoom )
	.zoom/active LDZ #00 = ,&skip-zoom JCN
		.Controller/button DEI #f0 AND
			DUP #04 SFT #01 AND #01 ! ,&no-up JCN
				( move ) .zoom/y LDZ2 #0001 -- .zoom/y STZ2 &no-up
			DUP #05 SFT #01 AND #01 ! ,&no-down JCN
				( move ) .zoom/y LDZ2 #0001 ++ .zoom/y STZ2 &no-down
			DUP #06 SFT #01 AND #01 ! ,&no-left JCN
				( move ) .zoom/x LDZ2 #0001 -- .zoom/x STZ2 &no-left
			DUP #07 SFT #01 AND #01 ! ,&no-right JCN
				( move ) .zoom/x LDZ2 #0001 ++ .zoom/x STZ2 &no-right
		#00 = #04 JCN ;draw-canvas JSR2
	&skip-zoom

	.Controller/key DEI
	DUP #20 ! ,&no-space JCN
		( toggle zoom ) .zoom/active LDZ #00 = .zoom/active STZ ;redraw JSR2 &no-space
	DUP #08 ! ,&no-backspace JCN
		( erase ) ;clear JSR2 &no-backspace
	DUP #71 ! ,&no-qkey JCN
		( tool0 ) #00 .brush/tool STZ ;draw-toolpane JSR2 &no-qkey
	DUP #77 ! ,&no-wkey JCN
		( tool0 ) #01 .brush/tool STZ ;draw-toolpane JSR2 &no-wkey
	DUP #65 ! ,&no-ekey JCN
		( tool0 ) #02 .brush/tool STZ ;draw-toolpane JSR2 &no-ekey
	DUP #72 ! ,&no-rkey JCN
		( tool0 ) #03 .brush/tool STZ ;draw-toolpane JSR2 &no-rkey
	DUP #74 ! ,&no-tkey JCN
		( tool0 ) #04 .brush/tool STZ ;draw-toolpane JSR2 &no-tkey
	DUP
		DUP #30 > SWP #39 < #0101 !! ,&no-numkey JCN
		( size ) .Controller/key DEI #31 SUB .brush/size STZ ;draw-sizepane JSR2 &no-numkey
	POP

BRK

@on-button-trap ( -> )

	( enter )
	.Controller/key DEI #0d ! ,&no-edit-enter JCN
		#00 .state/trap STZ
		#21 ;draw-filepath JSR2
		BRK &no-edit-enter
	( backspace )
	.Controller/key DEI #08 ! ,&no-edit-backspace JCN
		.path/length LDZ #00 = ,&edit-end JCN
		.path/length LDZ #01 - .path/length STZ
		#00 .path/name .path/length LDZ + STZ
		#21 ;draw-filepath JSR2
		BRK &no-edit-backspace
	( default )
	.path/length LDZ #1f = ,&edit-end JCN
		.Controller/key DEI .path/name .path/length LDZ + STZ
		.path/length LDZ #01 + .path/length STZ
	&edit-end
	#00 .path/name .path/length LDZ + STZ

BRK

@center ( -- )
	
	( clear old )
	.canvas/x1 LDZ2 #0002 -- 
	.canvas/y1 LDZ2 #0002 -- 
	.canvas/x2 LDZ2 #0002 ++ 
	.canvas/y2 LDZ2 #0002 ++ 
		#00 ;fill-rect JSR2

	;draw-background JSR2

	.Screen/width DEI2 #0002 // .canvas/w LDZ2 #0030 SFT2 #0001 SFT2 -- .canvas/x1 STZ2
	.Screen/height DEI2 #0002 // .canvas/h LDZ2 #0030 SFT2 #0001 SFT2 -- .canvas/y1 STZ2

	;fit-canvas JSR2
	;draw-foreground JSR2

RTN

@clear ( -- )
	
	;data FILESIZE ;data ++
	&loop
		( write ) OVR2 #00 ROT ROT STA
		( incr ) INCR2
		LTH2k ,&loop JCN
	POP2 POP2
	;redraw JSR2

RTN

@present ( -- )

	.state/presentation LDZ #00 = .state/presentation STZ

	.state/presentation LDZ
	DUP ,&skip0 JCN
		.theme/r0 LDZ2 .System/r DEO2 
		.theme/g0 LDZ2 .System/g DEO2 
		.theme/b0 LDZ2 .System/b DEO2 &skip0
	DUP #01 ! ,&skip1 JCN
		.theme/r1 LDZ2 .System/r DEO2 
		.theme/g1 LDZ2 .System/g DEO2 
		.theme/b1 LDZ2 .System/b DEO2 &skip1
	POP

	;draw-background JSR2
	;draw-canvas JSR2
	.canvas/w LDZ2 #0030 SFT2 .canvas/x1 LDZ2 ++ .canvas/x2 STZ2
	.canvas/h LDZ2 #0030 SFT2 .canvas/y1 LDZ2 ++ .canvas/y2 STZ2
	.canvas/x1 LDZ2 #0001 -- .canvas/y1 LDZ2 #0001 -- .canvas/x2 LDZ2 .canvas/y2 LDZ2 #01 ;line-rect JSR2
	;draw-foreground JSR2

RTN

@fit-canvas ( -- )
	
	.canvas/w LDZ2 #0030 SFT2 .canvas/x1 LDZ2 ++ .canvas/x2 STZ2
	.canvas/h LDZ2 #0030 SFT2 .canvas/y1 LDZ2 ++ .canvas/y2 STZ2
	.canvas/x1 LDZ2 #0001 -- 
	.canvas/y1 LDZ2 #0001 -- 
	.canvas/x2 LDZ2 
	.canvas/y2 LDZ2 
		#01 ;line-rect JSR2

	;draw-canvas JSR2

RTN

@paint-line ( x1* y1* x2* y2* -- )
	
	( load ) .line/y1 STZ2 .line/x1 STZ2 .line/y2 STZ2 .line/x2 STZ2

	( trim if zoomed )
	.zoom/active LDZ #01 ! ,&no-zoom JCN
		.line/x1 LDZ2 #0003 SFT2 .zoom/x LDZ2 ++ .line/x1 STZ2
		.line/y1 LDZ2 #0003 SFT2 .zoom/y LDZ2 ++ .line/y1 STZ2
		.line/x2 LDZ2 #0003 SFT2 .zoom/x LDZ2 ++ .line/x2 STZ2
		.line/y2 LDZ2 #0003 SFT2 .zoom/y LDZ2 ++ .line/y2 STZ2
	&no-zoom

	.line/x1 LDZ2 .line/x2 LDZ2 -- ABS2 .line/dx STZ2
	.line/y1 LDZ2 .line/y2 LDZ2 -- ABS2 #0000 SWP2 -- .line/dy STZ2
	#ffff #00 .line/x2 LDZ2 .line/x1 LDZ2 LTS2 #0002 ** ++ .line/sx STZ2
	#ffff #00 .line/y2 LDZ2 .line/y1 LDZ2 LTS2 #0002 ** ++ .line/sy STZ2
	.line/dx LDZ2 .line/dy LDZ2 ++ .line/e1 STZ2
	&loop
		( paint ) .line/x2 LDZ2 .line/y2 LDZ2 .brush/oper LDZ2 JSR2
		.line/x2 LDZ2 .line/x1 LDZ2 == .line/y2 LDZ2 .line/y1 LDZ2 == #0101 == ,&end JCN
		.line/e1 LDZ2 #0002 ** .line/e2 STZ2
		.line/e2 LDZ2 .line/dy LDZ2 LTS2 ,&skipy JCN
			.line/e1 LDZ2 .line/dy LDZ2 ++ .line/e1 STZ2
			.line/x2 LDZ2 .line/sx LDZ2 ++ .line/x2 STZ2
		&skipy
		.line/e2 LDZ2 .line/dx LDZ2 GTS2 ,&skipx JCN
			.line/e1 LDZ2 .line/dx LDZ2 ++ .line/e1 STZ2
			.line/y2 LDZ2 .line/sy LDZ2 ++ .line/y2 STZ2
		&skipx
		;&loop JMP2
	&end

	;draw-canvas JSR2

RTN

@paint-rect ( x1* y1* x2* y2* -- )

	( load ) .rect/y2 STZ2 .rect/x2 STZ2 .rect/y1 STZ2 .rect/x1 STZ2

	( trim if zoomed )
	.zoom/active LDZ #01 ! ,&no-zoom JCN
		.rect/x1 LDZ2 #0003 SFT2 .zoom/x LDZ2 ++ .rect/x1 STZ2
		.rect/y1 LDZ2 #0003 SFT2 .zoom/y LDZ2 ++ .rect/y1 STZ2
		.rect/x2 LDZ2 #0003 SFT2 .zoom/x LDZ2 ++ #0001 ++ .rect/x2 STZ2
		.rect/y2 LDZ2 #0003 SFT2 .zoom/y LDZ2 ++ #0001 ++ .rect/y2 STZ2
	&no-zoom

	.rect/x1 LDZ2 .pt2/x STZ2
	.rect/y1 LDZ2 .pt2/y STZ2

	&ver
		.rect/x1 LDZ2 .pt2/x STZ2
		&hor
			.pt2/x LDZ2 TOB .px/x STZ .pt2/y LDZ2 TOB .px/y STZ
			;patternize JSR2 #00 = ,&no-pixel JCN
				( draw ) .pt2/x LDZ2 .pt2/y LDZ2 .brush/oper LDZ2 JSR2 &no-pixel
			( incr ) .pt2/x LDZ2 #0001 ++ .pt2/x STZ2
			.pt2/x LDZ2 .rect/x2 LDZ2 LTS2 ,&hor JCN
		.pt2/y LDZ2 #0001 ++ .pt2/y STZ2
		.pt2/y LDZ2 .rect/y2 LDZ2 LTS2 ,&ver JCN

	;draw-canvas JSR2
	;draw-foreground JSR2
	
RTN

@paint-brush ( x* y* -- )

	#0003 -- .pt0/y STZ2 #0003 -- .pt0/x STZ2 ( cursor offset )

	( trim if zoomed )
	.zoom/active LDZ #01 ! ,&no-zoom JCN
		.pt0/x LDZ2 #0003 SFT2 .zoom/x LDZ2 ++ #0003 -- .pt0/x STZ2
		.pt0/y LDZ2 #0003 SFT2 .zoom/y LDZ2 ++ #0003 -- .pt0/y STZ2
	&no-zoom

	#00 .px/x STZ #00 .px/y STZ
	&ver
		#00 .px/x STZ
		&hor
			( addr ) ;size-icns #00 .brush/size LDZ #0030 SFT2 ++
			( byte ) #00 .px/y LDZ ++ LDA #07 .px/x LDZ SUB SFT #01 AND
			#00 = ,&no-pixel JCN
			;patternize JSR2 #00 = ,&no-pixel JCN
				.pt0/x LDZ2 #00 .px/x LDZ ++ .pt0/y LDZ2 #00 .px/y LDZ ++ .brush/oper LDZ2 JSR2 &no-pixel
			( incr ) .px/x LDZ #01 ADD .px/x STZ
			.px/x LDZ #08 < ,&hor JCN
		( incr ) .px/y LDZ #01 ADD .px/y STZ
		.px/y LDZ #08 < ,&ver JCN

	;draw-canvas JSR2
	;draw-foreground JSR2

RTN

@patternize ( -- )

	.brush/patt LDZ ,&noplain JCN
		#01 RTN &noplain

	.brush/patt LDZ #01 ! ,&notone1 JCN
		.pt0/x LDZ2 #00 .px/x LDZ ++ .pt0/y LDZ2 #00 .px/y LDZ ++ ++ #0001 AND2 #0000 ==
		.pt0/x LDZ2 #00 .px/x LDZ ++ .pt0/y LDZ2 #00 .px/y LDZ ++ -- #0001 AND2 #0000 ==
		#0101 ==
		RTN &notone1

	.brush/patt LDZ #02 ! ,&notone2 JCN
		.pt0/x LDZ2 #00 .px/x LDZ ++ .pt0/y LDZ2 #00 .px/y LDZ ++ ++ #0003 AND2 #0000 ==
		.pt0/x LDZ2 #00 .px/x LDZ ++ .pt0/y LDZ2 #00 .px/y LDZ ++ -- #0003 AND2 #0000 ==
		#0101 ==
		RTN &notone2

	.brush/patt LDZ #03 ! ,&notone3 JCN
		.pt0/x LDZ2 #00 .px/x LDZ ++ .pt0/y LDZ2 #00 .px/y LDZ ++ ++ #0005 AND2 #0000 ==
		.pt0/x LDZ2 #00 .px/x LDZ ++ .pt0/y LDZ2 #00 .px/y LDZ ++ -- #0005 AND2 #0000 ==
		#0101 ==
		RTN &notone3

	.brush/patt LDZ #04 ! ,&notone4 JCN
		.pt0/x LDZ2 #00 .px/x LDZ ++ .pt0/y LDZ2 #00 .px/y LDZ ++ ++ #0003 AND2 #0000 == RTN &notone4

	.brush/patt LDZ #05 ! ,&notone5 JCN
		.pt0/x LDZ2 #00 .px/x LDZ ++ .pt0/y LDZ2 #00 .px/y LDZ ++ -- #0003 AND2 #0000 == RTN &notone5

	.brush/patt LDZ #06 ! ,&notone6 JCN
		.pt0/x LDZ2 #00 .px/x LDZ ++ #0001 AND2 TOB RTN &notone6

	.brush/patt LDZ #07 ! ,&notone7 JCN
		.pt0/y LDZ2 #00 .px/y LDZ ++ #0001 AND2 TOB RTN &notone7

	#00

RTN

@get-pixel ( x* y* -- b )

	( clamp ) DUP2 #0008 // .canvas/h LDZ2 << #00 = ,&skip JCN
	( clamp ) OVR2 #0008 // .canvas/w LDZ2 << #00 = ,&skip JCN

	TOB #07 AND .px/y STZ
	TOB #07 AND .px/x STZ
	( get tile ) .pt1/x LDZ2 #0003 SFT2 .pt1/y LDZ2 #0003 SFT2 .canvas/w LDZ2 ** ++ #0030 SFT2
	( add addr ) ;data ++
	#00 .px/y LDZ ++ LDA #07 .px/x LDZ SUB SFT #01 AND
	RTN
	&skip
	POP2 POP2 #01

RTN

@add-pixel ( x* y* -- )
	
	( clamp ) DUP2 #0008 // .canvas/h LDZ2 << #00 = ,&skip JCN
	( clamp ) OVR2 #0008 // .canvas/w LDZ2 << #00 = ,&skip JCN

	.pt1/y STZ2 .pt1/x STZ2
	( get tile addr ) ;data .pt1/x LDZ2 #0003 SFT2 .pt1/y LDZ2 #0003 SFT2 .canvas/w LDZ2 ** ++ #0030 SFT2 .pt1/y LDZ2 MOD8 ++ ++
	( load ) LDAk
	( mask ) #01 #07 .pt1/x LDZ2 MOD8 TOB SUB SFL ORA
	( save ) ROT ROT STA
	RTN
	&skip
	POP2 POP2

RTN

@remove-pixel ( x* y* -- )

	( clamp ) DUP2 #0008 // .canvas/h LDZ2 << #00 = ,&skip JCN
	( clamp ) OVR2 #0008 // .canvas/w LDZ2 << #00 = ,&skip JCN

	.pt1/y STZ2 .pt1/x STZ2
	( get tile addr ) ;data .pt1/x LDZ2 #0003 SFT2 .pt1/y LDZ2 #0003 SFT2 .canvas/w LDZ2 ** ++ #0030 SFT2 .pt1/y LDZ2 MOD8 ++ ++
	( load ) LDAk
	( mask ) #01 #07 .pt1/x LDZ2 MOD8 TOB SUB SFL #ff EOR AND
	( save ) ROT ROT STA
	RTN
	&skip
	POP2 POP2

RTN

( file )

@clear-file ( -- )
	
	;clear JSR2
	;blank-txt ;path/name ;strcpy JSR2
	;redraw JSR2

RTN

@load-file ( path* -- )

	.File/name DEO2 FILESIZE 
	.File/length DEO2 ;data 
	.File/load DEO2
	.File/name DEI2 ;path/name ;strcpy JSR2
	;draw-canvas JSR2

RTN

@save-file ( path* -- )

	.File/name DEO2 FILESIZE 
	.File/length DEO2 ;data 
	.File/save DEO2

RTN

( Drawing )

@redraw ( -- )
	
	;draw-background JSR2
	;draw-canvas JSR2
	;draw-foreground JSR2
		
RTN

@draw-canvas ( -- )
	
	.zoom/active LDZ #01 = ;draw-canvas-zoom JCN2

	.canvas/y1 LDZ2 .Screen/y DEO2
	;data .Screen/addr DEO2
	&ver
		.canvas/x1 LDZ2 .Screen/x DEO2
		&hor
			( draw ) #29 .Screen/color DEO
			( incr ) .Screen/x DEI2 #0008 ++ .Screen/x DEO2
			( incr ) .Screen/addr DEI2 #0008 ++ .Screen/addr DEO2
		.Screen/x DEI2 .canvas/x2 LDZ2 !! ,&hor JCN
		( incr ) .Screen/y DEI2 #0008 ++ .Screen/y DEO2
	.Screen/y DEI2 .canvas/y2 LDZ2 !! ,&ver JCN

RTN

@draw-canvas-zoom ( -- )
	
	.zoom/y LDZ2 .pt1/y STZ2
	.canvas/y1 LDZ2 .Screen/y DEO2
	;data .Screen/addr DEO2
	&ver
		.canvas/x1 LDZ2 .Screen/x DEO2
		.zoom/x LDZ2 .pt1/x STZ2
		&hor
			( incr ) ;bigpixel-icns #0008 #00 .pt1/x LDZ2 .pt1/y LDZ2 ;get-pixel JSR2 ** ++ .Screen/addr DEO2
			( draw ) #29 .Screen/color DEO
			( incr ) .Screen/x DEI2 #0008 ++ .Screen/x DEO2
			( incr ) .pt1/x LDZ2 #0001 ++ .pt1/x STZ2
		.Screen/x DEI2 .canvas/x2 LDZ2 !! ,&hor JCN
		( incr ) .Screen/y DEI2 #0008 ++ .Screen/y DEO2
		( incr ) .pt1/y LDZ2 #0001 ++ .pt1/y STZ2
	.Screen/y DEI2 .canvas/y2 LDZ2 !! ,&ver JCN

RTN

@draw-cursor ( -- )

	( clear brush size )
	.cursor/x LDZ2 #0003 -- .Screen/x DEO2 
	.cursor/y LDZ2 #0003 -- .Screen/y DEO2 
	#30 .Screen/color DEO

	( clear last cursor )
	.cursor/x LDZ2 .Screen/x DEO2 
	.cursor/y LDZ2 .Screen/y DEO2 
	#30 .Screen/color DEO

	( record cursor positions )
	.Mouse/x DEI2 .cursor/x STZ2 
	.Mouse/y DEI2 .cursor/y STZ2

	( draw size cursor )
	.brush/tool LDZ #01 ! ;&outside-canvas JCN2

	( outside canvas )
	.Mouse/x DEI2 .Mouse/y DEI2 .canvas ;within-rect JSR2 #00 = ;&outside-canvas JCN2
		.cursor/x LDZ2 #0003 -- .Screen/x DEO2 .cursor/y LDZ2 #0003 -- .Screen/y DEO2
		;brush-icns #00 .brush/size LDZ #0030 SFT2 ++ .Screen/addr DEO2
		#31 .Mouse/state DEI #02 * ADD .Screen/color DEO
		.Mouse/state DEI #00 = ,&outside-canvas JCN RTN
	&outside-canvas

	( draw new cursor )
	.cursor/x LDZ2 .Screen/x DEO2 
	.cursor/y LDZ2 .Screen/y DEO2
	;pointers-icns #00 .Controller/button DEI #02 = #0030 SFT2 ++ .Screen/addr DEO2
	#3f .Mouse/state DEI #01 = #0a * SUB .Screen/color DEO

RTN

@draw-toolpane ( -- )

	.state/presentation LDZ #00 = #01 JCN [ RTN ]

	( frame ) 	
	.toolpane/x1 LDZ2 #0001 -- .toolpane/y1 LDZ2 #0001 -- .toolpane/x2 LDZ2 .toolpane/y2 LDZ2 #00 ;line-rect JSR2
	.toolpane/x1 LDZ2 #0002 -- .toolpane/y1 LDZ2 #0002 -- .toolpane/x2 LDZ2 .toolpane/y2 LDZ2 #01 ;line-rect JSR2

	.toolpane/x1 LDZ2 .Screen/x DEO2 .toolpane/y1 LDZ2 .Screen/y DEO2 ;tool-icns .Screen/addr DEO2

	&loop
		( draw ) #21 .Screen/x DEI2 .toolpane/x1 LDZ2 -- #0003 SFT2 TOB .brush/tool LDZ = #01 * ADD .Screen/color DEO
		( incr ) .Screen/x DEI2 #0008 ++ .Screen/x DEO2
		( incr ) .Screen/addr DEI2 #0008 ++ .Screen/addr DEO2
		.Screen/x DEI2 .toolpane/x2 LDZ2 << ,&loop JCN

	.zoom/active LDZ #01 ! ,&no-zoom JCN
		.Screen/x DEI2 #0008 -- .Screen/x DEO2
		;tool-icns #0028 ++ .Screen/addr DEO2
		#21 #04 .brush/tool LDZ = #02 * ADD .Screen/color DEO
		&no-zoom

RTN

@draw-pattpane ( -- )

	.state/presentation LDZ #00 = #01 JCN [ RTN ]

	( frame ) 	
	.pattpane/x1 LDZ2 #0001 -- .pattpane/y1 LDZ2 #0001 -- .pattpane/x2 LDZ2 .pattpane/y2 LDZ2 #00 ;line-rect JSR2
	.pattpane/x1 LDZ2 #0002 -- .pattpane/y1 LDZ2 #0002 -- .pattpane/x2 LDZ2 .pattpane/y2 LDZ2 #01 ;line-rect JSR2

	.pattpane/x1 LDZ2 .Screen/x DEO2 .pattpane/y1 LDZ2 .Screen/y DEO2 ;patt-icns .Screen/addr DEO2

	&loop
		( draw ) #21 .Screen/x DEI2 .pattpane/x1 LDZ2 -- #0003 SFT2 TOB .brush/patt LDZ = #01 * ADD .Screen/color DEO
		( incr ) .Screen/x DEI2 #0008 ++ .Screen/x DEO2
		( incr ) .Screen/addr DEI2 #0008 ++ .Screen/addr DEO2
		.Screen/x DEI2 .pattpane/x2 LDZ2 << ,&loop JCN

RTN

@draw-sizepane ( -- )
	
	.state/presentation LDZ #00 = #01 JCN [ RTN ]

	( frame )
	.sizepane/x1 LDZ2 #0001 -- .sizepane/y1 LDZ2 #0001 -- .sizepane/x2 LDZ2 .sizepane/y2 LDZ2 #00 ;line-rect JSR2
	.sizepane/x1 LDZ2 #0002 -- .sizepane/y1 LDZ2 #0002 -- .sizepane/x2 LDZ2 .sizepane/y2 LDZ2 #01 ;line-rect JSR2
	.sizepane/x1 LDZ2 .Screen/x DEO2 
	.sizepane/y1 LDZ2 .Screen/y DEO2 
	;size-icns .brush/mode LDZ #40 * TOS ++ .Screen/addr DEO2
	&loop
		( draw ) #21 .Screen/x DEI2 .sizepane/x1 LDZ2 -- #0003 SFT2 TOB .brush/size LDZ = #01 * ADD .Screen/color DEO
		( incr ) .Screen/x DEI2 #0008 ++ .Screen/x DEO2
		( incr ) .Screen/addr DEI2 #0008 ++ .Screen/addr DEO2
		.Screen/x DEI2 .sizepane/x2 LDZ2 << ,&loop JCN

RTN

@draw-viewpane ( -- )
	
	.state/presentation LDZ #00 = #01 JCN [ RTN ]

	( frame )
	.viewpane/x1 LDZ2 #0001 -- .viewpane/y1 LDZ2 #0001 -- .viewpane/x2 LDZ2 .viewpane/y2 LDZ2 #00 ;line-rect JSR2
	.viewpane/x1 LDZ2 #0002 -- .viewpane/y1 LDZ2 #0002 -- .viewpane/x2 LDZ2 .viewpane/y2 LDZ2 #01 ;line-rect JSR2

	.viewpane/x1 LDZ2 .Screen/x DEO2 .viewpane/y1 LDZ2 .Screen/y DEO2 ;view-icns .Screen/addr DEO2

	&loop
		( draw ) #21 .Screen/color DEO
		( incr ) .Screen/x DEI2 #0008 ++ .Screen/x DEO2
		( incr ) .Screen/addr DEI2 #0008 ++ .Screen/addr DEO2
		.Screen/x DEI2 .viewpane/x2 LDZ2 << ,&loop JCN

RTN

@draw-foreground ( -- )
	
	;draw-toolpane JSR2
	;draw-pattpane JSR2
	;draw-sizepane JSR2
	;draw-viewpane JSR2

RTN

@draw-background ( -- )
	
	( draw size )
	.Screen/width DEI2 #00a0 -- .Screen/x DEO2
	#000e .Screen/y DEO2
	.canvas/w LDZ2 #0080 SFT2 .canvas/h LDZ2 ++ #21 ;draw-short JSR2

	( bottom )
	.Screen/height DEI2 #0010 -- .Screen/y DEO2
	#0010 .Screen/x DEO2
	;path/name #01 ;draw-label JSR2
	&clear
		#20 .Screen/color DEO
		.Screen/x DEI2 #0008 ++ DUP2 .Screen/x DEO2
		.Screen/width DEI2 << ,&clear JCN

	( draw hor line )
	#0000 
	.Screen/width DEI2 
	.Screen/height DEI2 #0002 // 
	.state/presentation LDZ #00 = 
		;line-hor JSR2

	( draw ver line )
	.Screen/width DEI2 #0002 // 
	#0000 
	.Screen/height DEI2 
	.state/presentation LDZ #00 = 
		;line-ver JSR2

	( file i/o )
	.Screen/height DEI2 #0010 -- .Screen/y DEO2
	.Screen/width DEI2 #0018 -- .Screen/x DEO2
	;save-icn .Screen/addr DEO2
	#21 .Screen/color DEO
	.Screen/x DEI2 #0010 -- .Screen/x DEO2
	;load-icn .Screen/addr DEO2
	#21 .Screen/color DEO
	.Screen/x DEI2 #0008 -- .Screen/x DEO2
	;make-icn .Screen/addr DEO2
	#21 .Screen/color DEO

RTN

@draw-filepath ( color -- )
	
	STH
	#0010 .Screen/x DEO2
	.Screen/height DEI2 #0010 -- .Screen/y DEO2
	;path/name
	&loop
		LDAk DUP #00 ! #20 * - TOS #0008 ** ;font ++ .Screen/addr DEO2 
		STHkr .Screen/color DEO
		.Screen/x DEI2 #0008 ++ .Screen/x DEO2
		#0001 ++
		LDAk #00 ! ,&loop JCN
	POP2
	POPr
	( clear last ) 
	#20 .Screen/color DEO

RTN

( Generics )

@within-rect ( x* y* rect -- flag )
	
	STH
	( y < rect.y1 ) DUP2 STHkr #02 ADD LDZ2 << ,&skip JCN
	( y > rect.y2 ) DUP2 STHkr #06 ADD LDZ2 >> ,&skip JCN
	SWP2
	( x < rect.x1 ) DUP2 STHkr LDZ2 << ,&skip JCN
	( x > rect.x2 ) DUP2 STHkr #04 ADD LDZ2 >> ,&skip JCN
	POP2 POP2 POPr
	#01 
RTN
	&skip
	POP2 POP2 POPr
	#00

RTN

@line-rect ( x1* y1* x2* y2* color -- )

	( load ) .color STZ DUP2 STH2 .rect/y2 STZ2 .rect/x2 STZ2 DUP2 STH2 .rect/y1 STZ2 .rect/x1 STZ2
	STH2r STH2r
	&ver
		( save ) OVR2 .Screen/y DEO2
		( draw ) .rect/x1 LDZ2 .Screen/x DEO2 .color LDZ DUP .Screen/color DEO
		( draw ) .rect/x2 LDZ2 .Screen/x DEO2 .Screen/color DEO
		( incr ) INCR2
		OVR2 OVR2 LTS2 ,&ver JCN
	POP2 POP2
	.rect/x1 LDZ2 .rect/x2 LDZ2
	&hor
		( save ) OVR2 .Screen/x DEO2
		( draw ) .rect/y1 LDZ2 .Screen/y DEO2 .color LDZ DUP .Screen/color DEO
		( draw ) .rect/y2 LDZ2 .Screen/y DEO2 .Screen/color DEO
		( incr ) INCR2
		OVR2 OVR2 #0001 ++ LTS2 ,&hor JCN
	POP2 POP2

RTN

@fill-rect ( x1 y1 x2 y2 color -- )
	
	.color STZ
	( x1 x2 y1 y2 ) ROT2 SWP2
	&ver
		( save ) OVR2 .Screen/y DEO2
		STH2 STH2 OVR2 OVR2
		&hor
			( save ) OVR2 .Screen/x DEO2
			( draw ) .color LDZ .Screen/color DEO
			( incr ) INCR2
			OVR2 OVR2 LTS2 ,&hor JCN
		POP2 POP2 STH2r STH2r
		( incr ) INCR2
		OVR2 OVR2 LTS2 ,&ver JCN
	POP2 POP2 POP2 POP2

RTN

@draw-short ( short* color -- )

	STH SWP 
	DUP #04 SFT TOS #0008 ** ;font-hex ++ .Screen/addr DEO2
	( draw ) STHkr .Screen/color DEO
	#0f AND TOS #0008 ** ;font-hex ++ .Screen/addr DEO2
	.Screen/x DEI2 #0008 ++ .Screen/x DEO2
	( draw ) STHkr .Screen/color DEO
	DUP #04 SFT TOS #0008 ** ;font-hex ++ .Screen/addr DEO2
	.Screen/x DEI2 #0008 ++ .Screen/x DEO2
	( draw ) STHkr .Screen/color DEO
	#0f AND TOS #0008 ** ;font-hex ++ .Screen/addr DEO2
	.Screen/x DEI2 #0008 ++ .Screen/x DEO2
	( draw ) STHr .Screen/color DEO

RTN

@draw-label ( addr color -- )
	
	.color STZ
	&loop
		LDAk DUP #00 ! #20 * - TOS #0008 ** ;font ++ .Screen/addr DEO2 
		( draw ) .color LDZ #20 ADD .Screen/color DEO
		( incr ) #0001 ++
		( incr ) .Screen/x DEI2 #0008 ++ .Screen/x DEO2
		( loop ) LDAk ,&loop JCN
	POP2

RTN

@line-hor ( x0* x1* y* color -- )
	
	STH .Screen/y DEO2
	&loop
		( save ) OVR2 .Screen/x DEO2
		( draw ) STHkr .Screen/color DEO
		( incr ) SWP2 #0002 ++ SWP2
		LTH2k ,&loop JCN
	POP2 POP2 POPr

RTN

@line-ver ( x* y0* y1* color -- )
	
	STH ROT2 .Screen/x DEO2
	&loop
		( save ) OVR2 .Screen/y DEO2
		( draw ) STHkr .Screen/color DEO
		( incr ) SWP2 #0002 ++ SWP2
		LTH2k ,&loop JCN
	POP2 POP2 POPr

RTN

@strcpy ( src* dst* -- )

	&loop
		( copy src->dst ) OVR2 OVR2 SWP2 LDA ROT ROT STA
		( incr dst ) #0001 ++
		( incr src ) INCR2
		OVR2 LDA ,&loop JCN
	#00 ROT ROT STA POP2
	
RTN

@save-icn      1054 28c6 2854 1000 
@make-icn      fe82 8282 848a f400 
@load-icn      feaa d6aa d4aa f400 
@untitled-txt  "untitled.bit $1
@blank-txt     "blank.bit $1

@bigpixel-icns
	5580 0080 0080 0080 
	55ff 7fff 7fff 7fff 
@size-icns
	0000 0010 0000 0000
	0000 1038 1000 0000
	0000 3838 3800 0000
	0010 387c 3810 0000
	0038 7c7c 7c38 0000
	1038 7cfe 7c38 1000
	387c fefe fe7c 3800
	7cfe fefe fefe 7c00
	0000 0000 0000 0000
	0000 0010 0000 0000
	0000 1028 1000 0000
	0010 2844 2810 0000
	0038 4444 4438 0000
	1028 4482 4428 1000
	3844 8282 8244 3800
	7c82 8282 8282 7c00
@patt-icns
	fefe fefe fefe fe00
	fed6 aad6 aad6 fe00
	fe92 82d6 8292 fe00
	fe82 92aa 9282 fe00
	fea6 ca92 a6ca fe00
	feca a692 caa6 fe00
	feaa aaaa aaaa fe00	
	fe82 fe82 fe82 fe00
@tool-icns
	c0e0 5028 140a 0400
	e0d0 a844 2212 0c00
	c0b8 4848 7804 0200
	44ba 4444 44ba 4400
	3048 8484 4834 0200 ( zoom )
	3245 8284 4834 0200 ( zoom out )
@view-icns
	ee92 8a84 8a92 ee00
	f68a 92a2 928a f600
	fe82 8244 aa92 ee00
	fe82 92aa 4482 fe00
@brush-icns
	0000 0010 0000 0000
	0000 1028 1000 0000
	0000 3828 3800 0000
	0010 2844 2810 0000
	0038 4444 4438 0000
	1028 4482 4428 1000
	3844 8282 8244 3800
	7c82 8282 8282 7c00
	7cfe fefe fefe 7c00
@pointers-icns
	80c0 e0f0 f8e0 1000
	2020 20b8 7c7c 3838
@font-hex
	007c 8282 8282 827c 0030 1010 1010 1010
	007c 8202 7c80 80fe 007c 8202 1c02 827c
	000c 1424 4484 fe04 00fe 8080 7c02 827c
	007c 8280 fc82 827c 007c 8202 1e02 0202
	007c 8282 7c82 827c 007c 8282 7e02 827c
	007c 8202 7e82 827e 00fc 8282 fc82 82fc
	007c 8280 8080 827c 00fc 8282 8282 82fc
	007c 8280 f080 827c 007c 8280 f080 8080 
@font ( specter8-frag font )
	0000 0000 0000 0000 0008 0808 0800 0800
	0014 1400 0000 0000 0024 7e24 247e 2400
	0008 1e28 1c0a 3c08 0000 2204 0810 2200
	0030 4832 4c44 3a00 0008 1000 0000 0000
	0004 0808 0808 0400 0020 1010 1010 2000
	0000 2214 0814 2200 0000 0808 3e08 0800
	0000 0000 0000 0810 0000 0000 3e00 0000
	0000 0000 0000 0800 0000 0204 0810 2000
	003c 464a 5262 3c00 0018 0808 0808 1c00
	003c 4202 3c40 7e00 003c 421c 0242 3c00
	000c 1424 447e 0400 007e 407c 0242 3c00
	003c 407c 4242 3c00 007e 0204 0810 1000
	003c 423c 4242 3c00 003c 4242 3e02 3c00
	0000 0010 0000 1000 0000 1000 0010 1020
	0000 0810 2010 0800 0000 003e 003e 0000
	0000 1008 0408 1000 003c 420c 1000 1000
	003c 4232 4a42 3c00 003c 4242 7e42 4200
	007c 427c 4242 7c00 003c 4240 4042 3c00
	007c 4242 4242 7c00 007e 4078 4040 7e00
	007e 4078 4040 4000 003c 4240 4642 3c00
	0042 427e 4242 4200 001c 0808 0808 1c00
	007e 0202 0242 3c00 0042 4478 4442 4200
	0040 4040 4040 7e00 0042 665a 4242 4200
	0042 6252 4a46 4200 003c 4242 4242 3c00
	007c 4242 7c40 4000 003c 4242 4244 3a00
	007c 4242 7c44 4200 003e 403c 0242 3c00
	007e 0808 0808 1000 0042 4242 4244 3a00
	0042 4242 4224 1800 0042 4242 5a66 4200
	0042 423c 4242 4200 0042 423e 0242 3c00
	007e 020c 3040 7e00 000c 0808 0808 0c00
	0040 2010 0804 0200 0030 1010 1010 3000
	0008 1400 0000 0000 0000 0000 0000 7e00
	0008 0400 0000 0000 0000 3c02 3e42 3a00
	0040 407c 4242 7c00 0000 3c42 4042 3c00
	0002 023e 4242 3e00 0000 3c42 7e40 3e00
	0000 3e40 7840 4000 0000 3c42 3e02 3c00
	0040 405c 6242 4200 0008 0018 0808 0400
	0008 0018 0808 4830 0040 4244 7844 4200
	0010 1010 1010 0c00 0000 6c52 5252 5200
	0000 5c62 4242 4200 0000 3c42 4242 3c00
	0000 7c42 427c 4040 0000 3e42 423e 0202
	0000 5c62 4040 4000 0000 3e40 3c02 7c00
	0008 7e08 0808 1000 0000 4242 4244 3a00
	0000 4242 4224 1800 0000 5252 5252 2e00
	0000 4224 1824 4200 0000 4242 3e02 7c00
	0000 7e02 3c40 7e00 000c 0810 1008 0c00
	0008 0808 0808 0800 0030 1008 0810 3000
	0000 0032 4c00 0000 3c42 99a1 a199 423c

@data
